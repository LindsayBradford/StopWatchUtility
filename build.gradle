apply plugin: 'java'
apply plugin: 'application'

defaultTasks 'distribute'

// Start Project configuration

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

project.group = 'blacksmyth.stopwatch'
project.version = '1.0'

project.ext {
  title = 'StopWatchUtility'
  contact = 'Lindsay Willian Bradford (lindsay.w.bradford_at_gmail.com)'
  libsDirectory = 'libs'
  distributionDirectory = 'dist'
}

mainClassName = project.group + '.' + project.title

repositories {
   mavenCentral()
   maven {
     url 'http://mvn.erichseifert.de/maven2'
   } 
}

List libsToMerge = [ 
  'org.springframework:spring-context:4.2.5.RELEASE'
]

List testLibs = [
  'junit:junit:4.12', 
  'org.mockito:mockito-all:1.10.19'
]

configurations {
  mergeLibs
}

dependencies {
  mergeLibs   libsToMerge
  compile     libsToMerge
  testCompile testLibs
}

jar {

  from {
    configurations.mergeLibs.collect { zipTree(it) }
  }

  manifest {
   attributes (
      'Implementation-Title': project.title, 
      'Implementation-Version': project.version,
      'Contact':  project.contact,
      'Built-With': "gradle-${project.getGradle().getGradleVersion()}, groovy-${GroovySystem.getVersion()}",
      'Created-By': System.getProperty('java.version') + ' (' + System.getProperty('java.vendor') + ')',
      'Main-Class' : mainClassName
    )
  }
  
  doLast {
    file(jar.archivePath).setExecutable(true)
    println "$jar.archiveName created and is executable."
  }
}

task syncLibs(type: Sync) {
  from configurations.compile, configurations.testCompile
  into project.libsDirectory
  doLast {
    println "Synchronised library dependencies into directory ($project.libsDirectory)."
  }
}

task distribute(type: Copy) {
  dependsOn jar, syncLibs
  from jar 
  into project.distributionDirectory
  doLast {
    println "Distributed $project.title to directory ($project.distributionDirectory)."
  }
}

test {
  testLogging {
    events 'passed', 'skipped', 'failed'
  }
}

run {
  dependsOn distribute 
  doLast {
    println "Running $project.title"
  }
}

task full {
  dependsOn clean, distribute 
}
